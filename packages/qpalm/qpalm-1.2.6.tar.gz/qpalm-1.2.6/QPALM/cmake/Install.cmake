include(GNUInstallDirs)

set(INSTALL_CMAKE_DIR "${CMAKE_INSTALL_LIBDIR}/cmake/QPALM")

# Add the qpalm library to the "export-set", install the library files
install(TARGETS qpalm qpalm-headers qpalm-obj qpalm_lax_warnings
    EXPORT QPALM_coreTargets
    RUNTIME DESTINATION "${CMAKE_INSTALL_BINDIR}"
        COMPONENT shlib
    LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
        COMPONENT shlib
    ARCHIVE DESTINATION "${CMAKE_INSTALL_LIBDIR}" 
        COMPONENT lib)

# Install the header files
install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/include/"
    DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}"
        COMPONENT dev
    FILES_MATCHING REGEX "/.*\.[hti](pp)?$")

# Install the export set for use with the install tree
install(EXPORT QPALM_coreTargets 
    FILE QPALM_component_coreTargets.cmake
    DESTINATION "${INSTALL_CMAKE_DIR}" 
        COMPONENT dev
    NAMESPACE ${PROJECT_NAME}::)

# Generate the config file that includes the exports
include(CMakePackageConfigHelpers)
configure_package_config_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/cmake/Config.cmake.in"
    "${PROJECT_BINARY_DIR}/QPALMConfig.cmake"
    INSTALL_DESTINATION "${INSTALL_CMAKE_DIR}"
    NO_SET_AND_CHECK_MACRO)
configure_package_config_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/cmake/coreConfig.cmake.in"
    "${PROJECT_BINARY_DIR}/QPALM_component_coreConfig.cmake"
    INSTALL_DESTINATION "${INSTALL_CMAKE_DIR}"
    NO_SET_AND_CHECK_MACRO
    NO_CHECK_REQUIRED_COMPONENTS_MACRO)
write_basic_package_version_file(
    "${PROJECT_BINARY_DIR}/QPALMConfigVersion.cmake"
    VERSION "${PROJECT_VERSION}"
    COMPATIBILITY SameMajorVersion)

# Install the QPALMConfig.cmake and QPALMConfigVersion.cmake
install(FILES
    "${PROJECT_BINARY_DIR}/QPALMConfig.cmake"
    "${PROJECT_BINARY_DIR}/QPALMConfigVersion.cmake"
    "${PROJECT_BINARY_DIR}/QPALM_component_coreConfig.cmake"
    DESTINATION "${INSTALL_CMAKE_DIR}" 
        COMPONENT dev)

# Add all targets to the build tree export set
export(EXPORT QPALM_coreTargets
    FILE "${PROJECT_BINARY_DIR}/QPALM_component_coreTargets.cmake"
    NAMESPACE ${PROJECT_NAME}::)
